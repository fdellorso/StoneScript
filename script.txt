func equip_melee()
  equipL sword *5
  equipR shield *5

func equip_dps()
  equipL sword *5
  equipR bigsword *4

func equip_dashing()
  equipL sword *5
  equipR shield *2 dashing

func equip_staff()
  equip staff *4

func equip_ranged()
  equip repeating crossbow *4

func equip_ranged_healing()
  equipL crossbow *4
  equipR ouroboros

func equip_ranged_shield()
  equipL crossbow *4
  equipR shield *5

func equip_armor_piercing()
  equipL bigsword *4
  equipR warhammer *3

func healwalk()
  // Pick up materials quickly with Star stone
  ?pickup.distance < 10
    equipL star
    equipR shield *10
  // Heal and gain armor while just walking
  // towards next objective. If 100% healed,
  // walk faster.
  :?target = waypoint | foe.distance > 22
    ?hp < maxhp
      equipL ouroboros
      equipR shield *10
    :
      equipL triskelion
      equipR shield *10

?loc = rocky
  equip shovel
  healwalk()
  
  // Pick up stones quickly with Star stone
  ?pickup.distance < 6
    star_ouroboros()

  // Boss: Dysangelos, phase 1 (base form)
  ?foe = dysangelos_bearer
    equipL sword
    equipR shield

  // Boss: Dysangelos, phase 2 (elemental form)
  ?foe = dysangelos_elementalist
    equipL sword
    equipR shield
    // TODO use the proper runes against
    // the current rune in the eye of the boss

  // Boss: Dysangels, phase 3 (perfect form)
  ?foe = dysangelos_perfected
    equipL sword
    equipR shield
    ?foe.armor > 0
      loadout 3
    // Their most powerful move deals 30 damage
    // points on 5* difficulty, so we need to
    // heal if we don't have enough health and
    // armor combined
    ?(hp + armor) < 31
      activate potion

?loc = deadwood
  // Small enemies
  ?foe = mosquito
    ?foe.distance >= 5
      equip_ranged()
    :
      equip_melee()
  :?foe = scarab
    equip_melee()
  :?foe = boss | foe = poena
    // Boss: Xyaloalgia, residue of Leuce
    // and Poena, its phase 2
    ?foe.distance >= 8
      equip_dashing()
    :
      equip_melee()
  ?hp <= 10
    activate potion
  healwalk()
  ?harvest.distance < 10
    // Cut down trees with the hatchet
    // using the fast-cutting when available
    equip hatchet
    ?harvest.distance <= 4
    ^& item.GetCooldown("hatchet") <= 0
      activate R

?loc = caves
  ?foe ! boss  // Small enemies
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_staff()
    :
      // Try to heal while attacking from afar
      equip_ranged_healing()
  :?foe = boss  // Bolesh, the cunning
    ?foe.distance > 10
      equip_dashing()
    :
      equip_melee()
  :?hp < 10
    activate potion
  healwalk()

?loc = undead_crypt_boss | loc = undead_crypt
  // Small enemies and boss: same strategy
  // just keep on using the vigor staff
  // and heal while walking between enemies
  equip_staff()
  ?hp < 10
    activate potion
  
  healwalk()

?loc = mine
  // Small enemies
  ?foe = master
  ^ | foe = elemental
  ^ | foe = geyser
  ^ | foe = walker
    ?foe.distance > 8
      equip_dashing()
    :?foe.armor > 0
      equip_armor_piercing()
    :
      equip_melee()
  :?hp < 10
    activate potion
  healwalk()

?loc = bronze_guardian
  ?time > 600 & foe.distance > 10
    // Guardian is now attackable from afar
    equip_ranged_shield()
  ?pos.y > 0 & foe.distance > 5
    // Guardian hits, we jump on its hand
    equip_dashing()
  :?foe.distance <= 5
    // We are on its arm, close to its face
    ?foe.armor > 0
      equip_armor_piercing()
    :
      equip_dps()
  :?hp < 10
    activate potion
  healwalk()

var hrminir_encountered = false
var blade_of_god_var = bigsword
  
?loc = icy_ridge
  // Small enemies
  ?foe = elemental | foe = ki
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_staff()
    :
      // Try to heal while attacking from afar
      equip_ranged_healing()
  :?foe = pillar | foe = ice_wall
    ?foe.distance <= 5
      equip blade_of_god_var
      ?hrminir_encountered &
      ^ item.GetCooldown("blade_of_god_var") <= 0
        // Hit the wall and the boss
        // behind it as well. Don't use `R`
        // before the boss to avoid waiting
        // for the cooldown.
        activate R
    :
      equip_dashing()

  // HrÃ­mnir
  :?foe = boss
    hrminir_encountered = true
    ?foe.distance <= 2
      ?foe.armor > 0
        equip_armor_piercing()
      :
        equip_dps()
    :?foe.distance <= 5
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    :?foe.distance > 5
      equip_dashing()

  healwalk()

?loc = temple
  equip_staff()
  ?hp <= 10
    activate potion

?loc = nagaraja
  equip blade_of_god_var
  ?hp <= 10 | debuffs.count >= 8
    activate potion
